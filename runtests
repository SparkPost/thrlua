#!/usr/bin/perl -w
# Copyright (c) 2008-2010 Message Systems, Inc.
# Runs unit tests
# vim:ts=2:sw=2:et:
use strict;
use File::Find;
use Data::Dumper;
use Getopt::Long;
use lib 't';
use t::strap;
BEGIN {
  $ENV{HARNESS_STRAP_CLASS} = "t::strap";
}
use Test::Harness;

my $use_installed = 0;
my $quiet = 0;
my $valgrind = 0;

GetOptions(
  "compare" => \$use_installed,
  "valgrind|g" => \$valgrind,
  "quiet" => \$quiet
);

# in order to run sensibly on different versions of perl, we use the old
# way of poking the strap (so that we can run lua bits intelligently).
# we have to save the callback that was set on the default strap and
# attach that to the new strap for test output to show up
my $strap_callback = $Test::Harness::Strap->{callback};
$Test::Harness::Strap = t::strap->new;
$Test::Harness::Strap->{callback} = $strap_callback;
$Test::Harness::verbose = !$quiet;
$ENV{'TEST_VERBOSE'} = 1 if !$quiet;
$ENV{'HARNESS_VERBOSE'} = 1 if !$quiet;
$ENV{'USE_VALGRIND'} = 1 if $valgrind;

use Config;
$ENV{RCLUA_INIT} = <<LUA;
platform = {
  osname=[[$Config{osname}]],
}
LUA

$ENV{RCLUA_PATH} = './?.lua;./etc/?.lua;./etc/?/init.lua';
$ENV{RCLUA_CPATH} = ".libs/?.so";
if ($use_installed) {
  $ENV{LUA_EXECUTABLE} ||= "/opt/msys/3rdParty/bin/rclua";
} else {
  $ENV{DYLD_LIBRARY_PATH} = ".libs";
  $ENV{LD_LIBRARY_PATH} = ".libs";
  $ENV{LUA_EXECUTABLE} ||= ".libs/rclua";
}
$ENV{THIS_ENV_VAR_IS_SET_FOR_THE_ENV_TEST} = "w00t";

my @tests;
@ARGV = "t" unless @ARGV;
foreach (@ARGV) {
  find(sub {
    next if $_ eq '.hg';
    next if $_ eq '.svn';
    next if $_ eq 'CVS';
    if (m/\.(lua|t|luat)$/ and -f $_) {
      push @tests, $File::Find::name;
    }
  }, $_);
}
runtests(@tests);

